config {
    tags: ["process"],
    type: "table",
    description: "A table for storing lifetime customer summaries",
    columns: {
        account_number: "Account Number as defined in the source system.",
        lifetime_withdrawals: "Sum of all successful withdrawals",
        lifetime_deposits: "Sum of all successful deposits",
        latest_balance: "Most recent balance amount for the account",
        starting_balance: "Earliest balance amount for the account"
    },
    schema: `${dataform.projectConfig.vars.dataset_id_exposure}`,
    database: `${dataform.projectConfig.vars.dataset_projectid_exposure}`,
    name: "customer_lifetime_value"
}
SELECT
    t.account_number,
    sum(t.withdrawal_amount) as lifetime_withdrawals,
    sum(t.deposit_amount) as lifetime_deposits,
    max(case when t.transaction_rank = 1 then balance else null end) as latest_balance,
    max(case when t.transaction_reverse_rank = 1 then balance else null end) as starting_balance,
    max(case when t.transaction_rank = 1 then date else null end) as latest_transaction_date,
    max(case when t.transaction_reverse_rank = 1 then date else null end) as earliest_transaction_date
FROM
   ${ref("curated_transactions")} t
group by 1